plugins {
    id "java"
    id "java-library"
    id "maven-publish"
}

repositories {
    mavenLocal()
    jcenter()
    maven {
        url "https://repo.conduit.systems/repository/releases/"
    }
}

dependencies {
    api "org.projectlombok:lombok:1.18.10"
    annotationProcessor "org.projectlombok:lombok:1.18.10"
    api "org.apache.logging.log4j:log4j-api:2.8.1"
    api "com.google.code.gson:gson:2.8.0"
    api "us.tedstar:Mojang2Tsrg:1.0.0"
    api "cpw.mods:modlauncher:4.2.0"
    api "org.spongepowered:mixin:0.8"
}

group = theGroup
version = theVersion
sourceCompatibility = "1.8"

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

task install(dependsOn: [publishToMavenLocal])

java {
    withSourcesJar()
}

jar {
    manifest {
        attributes(
                "Main-Class" : "systems.conduit.launcher.MainStart",
                "Launcher-Agent-Class" : "systems.conduit.launcher.Agent"
        )
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            credentials {
                username System.getProperty("MAVEN_USER")?:'ci'
                password System.getProperty("MAVEN_PASSWORD")?:'password'
            }
            url "https://repo.conduit.systems/repository/releases/"
        }
    }
}
